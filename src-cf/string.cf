~( Zero terminated + counted strings )
:s/length (a - n) ^@ ;
:s/count (a - a n) ^dup p@ swap cell + swap ;
:s/uncount (a n - a)  ^drop cell - ;
:s/type (a n -) ^swap dup pc@ emit 1+ swap 1- dup 0 = '2drop ^if; s/type ;
:s/type- (a n -) ^2dup s/type s/uncount c-free ;

:.s/key ^dup  echo pc@ 'emit 'drop ^if-else ;
:s/store ^over pc! 1+ ;

:s/done ^drop over cell + - over p! ;

:s" ^1024
:ns" (n - a) ^c-alloc dup cell +
:s/loop ^key .s/key dup ~key " ,l ^= 's/done ^if; s/store s/loop ;

:s+" (a - a) ^dup s/count + s/loop ;

:s, (a -) ^dup 0 1 dalloc c! (store 0)
  dup p@ cell + dup dalloc dup ,l
  &heap swap pcmove c-free ;

:c" ^s" s, ;

:pr ^&heap
:ppr ^s/count s/type ;

:ppr- ^s/count s/type- ;

:prn ^pr cr ;
:npr ^cr pr ;
:nprn ^cr pr cr ;

:pr0 (a - ) ^dup pc@ dup 0 = '2drop ^if; emit 1+ pr0 ;

~( Runtime strings )
:." ^key dup ~key " ,l ^= 'drop ^if; emit ." ;

~( string operations )
:addr ~var ^; :pos ~var ^; :len ~var ^; :addr2 ~var ^;

:locals! ^pos ! len ! addr ! ;
:p1-p2@ ^ addr @ cell + pos @ + (p1)
   dup len @ +  (p2) ;
:base-n@ ^addr @ p@ pos @ - ;
:terminate (xt - a) ^>R addr @ p@ len @ R> execute dup addr @ p! (new len)
   addr @ + cell + 0 swap pc! (0 terminated)
   addr @ ;

:s/insert (a len pos - a) ^locals!
   p1-p2@  base-n@  pcmove> '+ ^terminate ;

:s/cut (a len pos - a) ^locals!
   p1-p2@ swap   base-n@ len @ -  pcmove '- ^terminate ;

:s/copy (a a len pos - a) ^pos ! len ! addr2 ! addr !
   addr2 @   addr @ cell + pos @ +   len @   pcmove>
   addr @ ;

:s/ccopy ^>R s/count R> s/copy ;

~`addr ~e/hide `pos ~e/hide `len ~e/hide `addr2 ~e/hide
~`locals! ~e/hide `p1-p2@ ~e/hide
~`base-n@ ~e/hide `terminate ~e/hide
